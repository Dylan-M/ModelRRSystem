%* 
%* ------------------------------------------------------------------
%* GraphicsSupport.tex - Using the graphics support code.
%* Created by Robert Heller on Thu Apr 19 14:38:52 2007
%* ------------------------------------------------------------------
%* Modification History: $Log$
%* Modification History: Revision 1.2  2007/11/30 13:56:50  heller
%* Modification History: Novemeber 30, 2007 lockdown.
%* Modification History:
%* Modification History: Revision 1.1  2007/05/06 12:49:38  heller
%* Modification History: Lock down  for 2.1.8 release candidate 1
%* Modification History:
%* Modification History: Revision 1.1  2002/07/28 14:03:50  heller
%* Modification History: Add it copyright notice headers
%* Modification History:
%* ------------------------------------------------------------------
%* Contents:
%* ------------------------------------------------------------------
%*  
%*     Model RR System, Version 2
%*     Copyright (C) 1994,1995,2002-2005  Robert Heller D/B/A Deepwoods Software
%* 			51 Locke Hill Road
%* 			Wendell, MA 01379-9728
%* 
%*     This program is free software; you can redistribute it and/or modify
%*     it under the terms of the GNU General Public License as published by
%*     the Free Software Foundation; either version 2 of the License, or
%*     (at your option) any later version.
%* 
%*     This program is distributed in the hope that it will be useful,
%*     but WITHOUT ANY WARRANTY; without even the implied warranty of
%*     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
%*     GNU General Public License for more details.
%* 
%*     You should have received a copy of the GNU General Public License
%*     along with this program; if not, write to the Free Software
%*     Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
%* 
%*  
%* 

\chapter{Using the Graphics Support Code.}
\label{chapt:GraphicsSupport}
\typeout{$Id}

Version 2 of the Graphics Support is covered in this chapter.  Version
1 is depreciated and is included only for older applications.

The grsupport package defines the constants $\pi$ and $\pi \over 2$, a
procedure to convert degrees to radians, plus a collection of Snit
macros that define common validating methods that are useful when
defining Snit types.

\begin{lstlisting}[caption={Graphics Support package examples},
		   label={lst:GRS:Examples}]
package require snit;#	Load the snit package
package require Tk;#	Load the Tk package
package require grsupport 2.0;#	Load V2 Graphics Support package.

snit::type Circle {
  # Snit type for drawing circles.

  #*********************
  # Methods:
  #*********************
  GRSupport::VerifyDoubleMethod;# Verify double valued options.
  GRSupport::VerifyColorMethod;#  Verify color valued options.
  method _ConfigureXY {option value} {
  # Method to configure X or Y.
  # <in> option The name of the option to configure.
  # <in> value The new value.
  # [index] _ConfigureXY!method

    set oldx $options(-x)
    set oldy $options(-y)
    set options($option) $value
    set dx [expr {$oldx - $options(-x)}]
    set dy [expr {$oldy - $options(-y)}]
    $canvas move $selfns $dx $dy
    set x $options(-x)
    set y $options(-y)
    set size $options(-size)
    set sx [expr {$x + $size}]
    set sy [expr {$y + $size}]
    set centerX [expr {$x + ($size * 0.5)}]
    set centerY [expr {$y + ($size * 0.5)}]
  }
  method _ConfigureSize {option value} {
  # Method to configure size.
  # <in> option The name of the option to configure.
  # <in> value The new value.
  # [index] _ConfigureSize!method

    set deltaSize [expr {$options($option) - $value}]
    set options($option) $value
    $canvas scale $selfns $options(-x) $options(-y) \
				$deltaSize $deltaSize
    set x $options(-x)
    set y $options(-y)
    set size $options(-size)
    set sx [expr {$x + $size}]
    set sy [expr {$y + $size}]
    set centerX [expr {$x + ($size * 0.5)}]
    set centerY [expr {$y + ($size * 0.5)}]
  }
  method _ConfigureFillColor {option value} {
  # Method to configure a fill color.
  # <in> option The name of the option to configure.
  # <in> value The new value.
  # [index] _ConfigureFillColor!method

    set options($option) $value
    set  tag $selfns
    catch {$canvas itemconfigure ${tag}$option -fill "$value"}
  }
  method _ConfigureOutlineColor {option value} {
  # Method to configure an outline color.
  # <in> option The name of the option to configure.
  # <in> value The new value.
  # [index] _ConfigureOutlineColor!method

    set options($option) $value
    set  tag $selfns
    catch {$canvas itemconfigure ${tag}$option -outline "$value"}
  }
  method Circumfrence {} {
    # Method to return the circumfrence of the circle.
    # Uses PI from the Graphics Support library.
    return [expr {$options(-size) * $::GRSupport::PI}]
  }
  #***************************
  # Variables:
  #***************************
  variable canvas;#	Canvas the circle is drawn on.
  variable sx;#		Right side of circle.
  variable sy;#		Bottom side of circle.
  variable centerX;#	Center of circle (X).
  variable centerY;#	Center of circle (Y).

  #***************************
  # Options:
  #***************************
  # Upper left corner (x,y).
  option -x -default 0 -validatemethod _VerifyDouble \
			 -configuremethod _ConfigureXY
  option -y -default 0 -validatemethod _VerifyDouble \
			-configuremethod _ConfigureXY
  # Size (diameter) of circle.
  option -size -default 100 -validatemethod _VerifyDouble \
			    -configuremethod _ConfigureSize
  # Color of the circle.
  option -outline -default black -validatemethod _VerifyColor \
				-configuremethod _ConfigureOutlineColor
  option -fill -default black -validatemethod _VerifyColor \
				-configuremethod _ConfigureFillColor
  #***************************
  # Constructor: draw the circle.
  #***************************
  constructor {_canvas args} {
    set canvas $_canvas
    set tag $selfns
    set x $options(-x)
    set y $options(-y)
    set size $options(-size)
    set sx [expr {$x + $size}]
    set sy [expr {$y + $size}]
    catch {$canvas delete $tag}
    $canvas create oval $x $y $sx $sy \
		-outline "$options(-outline)" \
		-fill    "$options(-fill)" \
		-width   2 \
		-tag     [list $tag ${tag}-fill \
				${tag}-outline]
  }
  #***************************
  # Descructor: remove the circle.
  #***************************
  destructor {
    catch {$canvas delete $selfns}
  }
}
\end{lstlisting}
Listing~\ref{lst:GRS:Examples} shows some code that uses the Graphics
Support package.
