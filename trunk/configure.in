dnl * 
dnl * ------------------------------------------------------------------
dnl * configure.in - Configure script
dnl * Created by Robert Heller on Sat Aug  5 05:52:33 2006
dnl * ------------------------------------------------------------------
dnl * Modification History: $Log$
dnl * Modification History: Revision 1.44  2007/11/30 14:13:13  heller
dnl * Modification History: 11-30-2007 Lockdown
dnl * Modification History:
dnl * Modification History: Revision 1.43  2007/11/30 13:56:35  heller
dnl * Modification History: Novemeber 30, 2007 lockdown.
dnl * Modification History:
dnl * Modification History: Revision 1.42  2007/11/03 20:26:54  heller
dnl * Modification History: Release 2.1.11
dnl * Modification History:
dnl * Modification History: Revision 1.41  2007/10/22 17:17:26  heller
dnl * Modification History: 10222007
dnl * Modification History:
dnl * Modification History: Revision 1.40  2007/09/30 15:49:28  heller
dnl * Modification History: Rev 2.1.10 Lockdown
dnl * Modification History:
dnl * Modification History: Revision 1.39  2007/09/03 14:39:28  heller
dnl * Modification History: Rev 2.1.9 Lockdown
dnl * Modification History:
dnl * Modification History: Revision 1.38  2007/05/06 12:49:38  heller
dnl * Modification History: Lock down  for 2.1.8 release candidate 1
dnl * Modification History:
dnl * Modification History: Revision 1.37  2007/04/19 17:23:20  heller
dnl * Modification History: April 19 Lock Down
dnl * Modification History:
dnl * Modification History: Revision 1.36  2007/02/21 20:48:37  heller
dnl * Modification History: SWIG Hackery
dnl * Modification History:
dnl * Modification History: Revision 1.35  2007/02/21 20:08:55  heller
dnl * Modification History: SWIG Hackery
dnl * Modification History:
dnl * Modification History: Revision 1.31  2007/02/01 20:38:25  heller
dnl * Modification History: Lock down for Release 2.1.7
dnl * Modification History:
dnl * Modification History: Revision 1.29  2007/01/08 23:19:21  heller
dnl * Modification History: Win32 build
dnl * Modification History:
dnl * Modification History: Revision 1.27  2007/01/08 20:11:32  heller
dnl * Modification History: Cross Compile Hacks
dnl * Modification History:
dnl * Modification History: Revision 1.26  2007/01/08 19:05:53  heller
dnl * Modification History: Jan 8, 2007 Lockdown
dnl * Modification History:
dnl * Modification History: Revision 1.1  2002/07/28 14:03:50  heller
dnl * Modification History: Add it copyright notice headers
dnl * Modification History:
dnl * ------------------------------------------------------------------
dnl * Contents:
dnl * ------------------------------------------------------------------
dnl *  
dnl *     Model RR System, Version 2
dnl *     Copyright (C) 1994,1995,2002-2012  Robert Heller D/B/A Deepwoods Software
dnl * 			51 Locke Hill Road
dnl * 			Wendell, MA 01379-9728
dnl * 
dnl *     This program is free software; you can redistribute it and/or modify
dnl *     it under the terms of the GNU General Public License as published by
dnl *     the Free Software Foundation; either version 2 of the License, or
dnl *     (at your option) any later version.
dnl * 
dnl *     This program is distributed in the hope that it will be useful,
dnl *     but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl *     GNU General Public License for more details.
dnl * 
dnl *     You should have received a copy of the GNU General Public License
dnl *     along with this program; if not, write to the Free Software
dnl *     Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
dnl * 
dnl *  
dnl * 

AC_INIT(MRRSystem,2.1.35, [support@deepsoft.com], MRRSystem)
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_SUBST(build)
AC_CANONICAL_TARGET
AC_SUBST(target)
AC_CONFIG_SRCDIR(C++/ParserClasses/mrr.i)
AM_INIT_AUTOMAKE([-Wno-portability])
MRR_VERSION=[`echo $VERSION|sed 's/\.[0-9][0-9]*[^0-9]*$//g'`]
AC_DEFINE([MRR_VERSION],$MRR_VERSION,[MRR Version])
MRR_MAJOR_VERSION=[`echo $MRR_VERSION|sed 's/\.[0-9][0-9]*[^0-9]*$//g'`]
AC_DEFINE([MRR_MAJOR_VERSION],$MRR_MAJOR_VERSION,[MRR Major])
MRR_MINOR_VERSION=[`echo $MRR_VERSION|sed 's/^[0-9][0-9]*\.//g'`]
AC_DEFINE([MRR_MINOR_VERSION],$MRR_MINOR_VERSION,[MRR Minor])
MRR_PATCH_LEVEL=[`echo $VERSION|sed 's/[^0-9]*$//g'`]
AC_DEFINE([MRR_PATCH_LEVEL],$MRR_PATCH_LEVEL,[MRR Patch Level])
MRR_PATCH_LEVELLIB=["$MRR_PATCH_LEVEL"]
AC_DEFINE([MRR_PATCH_LEVELLIB],$MRR_PATCH_LEVELLIB,[MRR Patch Level for libraries])
AC_SUBST(MRR_PATCH_LEVELLIB)
AC_SUBST(MRR_PATCH_LEVEL)
AC_SUBST(MRR_VERSION)
AC_SUBST(MRR_MAJOR_VERSION)
AC_SUBST(MRR_MINOR_VERSION)


dnl Force cross_compiling, even if it *looks* like we can run cross-built 
dnl binaries (get around wine's cleverness).
if test ${cross_compiling} = "maybe"; then
  if test ${target_os} != ${build_os}; then
    cross_compiling=yes
  fi
fi  


AC_CONFIG_MACRO_DIR([m4])
AC_DEFINE([CMRI_VERSION],["1.0.0"],[CMRI Version])
CMRI_VERSIONLIB=1.0.0
AC_SUBST(CMRI_VERSIONLIB)
AC_DEFINE([XPRESSNET_VERSION],["1.0.1"],[XPressNet Version])
XPRESSNET_VERSIONLIB=1.0.1
AC_SUBST(XPRESSNET_VERSIONLIB)
AC_DEFINE([FCFCLASSES_VERSION],["1.0.4"],[FCF Classes Version])
FCFCLASSES_VERSIONLIB=1.0.4
AC_SUBST(FCFCLASSES_VERSIONLIB)
AC_DEFINE([TTCLASSES_VERSION],["1.0.2"],[TT Classes Version])
TTCLASSES_VERSIONLIB=1.0.2
AC_SUBST(TTCLASSES_VERSIONLIB)
AC_DEFINE([MRD_VERSION],["1.0.2"],[MRD Version])
MRD_VERSIONLIB=1.0.2
AC_SUBST(MRD_VERSIONLIB)
AC_DEFINE([AZATRAX_VERSION],["1.0.0"],[AZATRAX Version])
AZATRAX_VERSIONLIB=1.0.0
AC_SUBST(AZATRAX_VERSIONLIB)
AC_DEFINE([RAILDRIVERIO_VERSION],["1.0.0"],[Raildriver IO Version])
RAILDRIVERIO_VERSIONLIB=1.0.0
AC_SUBST(RAILDRIVERIO_VERSIONLIB)

RELEASEDATE=[`echo '$Date$'|sed 's/^\$Date:[[:space:]]*[^(]*.\(.*\))[[:space:]]\$$/\1/g'`]
AC_SUBST(RELEASEDATE)

if test "x${prefix}" = "xNONE"; then
    prefix=/usr/local
fi
if test "x${exec_prefix}" = "xNONE"; then
    exec_prefix="${prefix}"
fi

BUILD_DIR=`pwd`
TOP_DIR=${BUILD_DIR}

AC_SUBST(TOP_DIR)
AC_SUBST(BUILD_DIR)

srcdir=`dirname $0`
cd $srcdir
SRC_DIR=`pwd`
cd $BUILD_DIR

AC_SUBST(SRC_DIR)

AC_SUBST(target)

dnl Checks for programs.

AC_GNU_SOURCE
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CC
AC_PROG_LEX
dnl if test ${cross_compiling} = "no"; then
dnl   AM_DISABLE_STATIC
dnl fi
AC_LIBTOOL_DLOPEN
if test ${cross_compiling} = "yes"; then
  lt_cv_deplibs_check_method='pass_all'
fi
AM_PROG_LIBTOOL
AX_PKG_DOXYGEN(1.6.0, [], [ AC_MSG_ERROR([DOXYGEN 1.6.0 is required to build.]) ])
AC_PATH_PROG(PDFLATEX,pdflatex,/bin/true)
AC_PATH_PROG(LATEX,latex,/bin/true)
AC_PATH_PROG(MAKEINDEX,makeindex,/bin/true)
AC_PATH_PROG(BIBTEX,bibtex,/bin/true)
AC_PATH_PROG(DVIPS,dvips,/bin/true)
AC_PATH_PROG(PS2PDF,ps2pdf,/bin/true)
AM_GNU_GETTEXT
AC_PATH_PROG(MSGEN,msgen)
AC_CHECK_HEADERS(libintl.h locale.h)
AX_PKG_SWIG(1.3)
AX_SWIG_ENABLE_CXX
AC_PATH_PROG(BISONXX,bison++,/bin/true)


AM_CONDITIONAL(DONT_HAVE_BISONXX,test $BISONXX = /bin/true)

if test $BISONXX = /bin/true; then
  AC_MSG_WARN([You don't have bison++ installed, not going to build XtrkCAD parser])
fi

dnl Checks for libraries.

PKG_PROG_PKG_CONFIG()
if test ${cross_compiling} = "yes"; then
  xcprefix1="`$CC -v 2>&1 | grep prefix`"
  dnl AC_MSG_NOTICE([*** xcprefix1 is ${xcprefix1}])
  xcprefix2="`echo ${xcprefix1}|sed 's/^.*--prefix=//g'`"
  dnl AC_MSG_NOTICE([*** xcprefix2 is ${xcprefix2}])
  xcprefix="`echo ${xcprefix2}|sed 's/[[[:space:]]].*$//g'`"
  dnl AC_MSG_NOTICE([*** xcprefix is ${xcprefix}])
  export PKG_CONFIG_PATH="${xcprefix}/lib/pkgconfig:${xcprefix}/share/pkgconfig"
  dnl AC_MSG_NOTICE([PKG_CONFIG_PATH is $PKG_CONFIG_PATH])
  m4_pattern_allow(PKG_CONFIG_LIBDIR)
  export PKG_CONFIG_LIBDIR="${xcprefix}/lib/pkgconfig:${xcprefix}/share/pkgconfig"
  dnl AC_MSG_NOTICE([PKG_CONFIG_LIBDIR is $PKG_CONFIG_PATH])
fi

PKG_CHECK_EXISTS(libusb-1.0,[have_libusb1="yes"],[have_libusb1="no"])
if test ${have_libusb1} = "yes"; then
  AC_DEFINE(HAVE_LIBUSB1,1, [Indicates if we have Libusb1 or not.])
  PKG_CHECK_MODULES([LIBUSB],libusb-1.0)
  AC_SUBST(HAVE_LIBUSB1)
  libusbVer=`pkg-config --modversion libusb-1.0`
  AC_MSG_RESULT([libusb Version is $libusbVer])
else
  AC_MSG_WARN([You don't have libusb-1.0 installed, some parts of the system won't be built.])
fi

AM_CONDITIONAL(DONT_HAVE_USB,test x$LIBUSB_CFLAGS = "x")

AM_CONDITIONAL(CROSSCOMPILING,test ${cross_compiling} = "yes")
if test ${cross_compiling} = "yes"; then
  LIB_MRR_PATCH_LEVELLIB=`echo $MRR_PATCH_LEVELLIB|sed 's/\./-/g'`
  CMRI_LIB_VERSIONLIB=`echo $CMRI_VERSIONLIB|sed 's/\./-/g'`
  XPRESSNET_LIB_VERSIONLIB=`echo $XPRESSNET_VERSIONLIB|sed 's/\./-/g'`
  FCFCLASSES_LIB_VERSIONLIB=`echo $FCFCLASSES_VERSIONLIB|sed 's/\./-/g'`
  TTCLASSES_LIB_VERSIONLIB=`echo $TTCLASSES_VERSIONLIB|sed 's/\./-/g'`
else
  LIB_MRR_PATCH_LEVELLIB=$MRR_PATCH_LEVELLIB
  CMRI_LIB_VERSIONLIB=$CMRI_VERSIONLIB
  XPRESSNET_LIB_VERSIONLIB=$XPRESSNET_VERSIONLIB
  FCFCLASSES_LIB_VERSIONLIB=$FCFCLASSES_VERSIONLIB
  TTCLASSES_LIB_VERSIONLIB=$TTCLASSES_VERSIONLIB
fi
AC_SUBST(LIB_MRR_PATCH_LEVELLIB)
AC_SUBST(CMRI_LIB_VERSIONLIB)
AC_SUBST(XPRESSNET_LIB_VERSIONLIB)
AC_SUBST(FCFCLASSES_LIB_VERSIONLIB)
AC_SUBST(TTCLASSES_LIB_VERSIONLIB)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/ioctl.h sys/time.h syslog.h unistd.h sys/select.h)

AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(int)
AX_CXX_BOOL
AC_MSG_CHECKING([stdc++])
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
savedLibs=$LIBS
LIBS=-lstdc++
AC_TRY_LINK([
#include <iostream.h>
],[
	cout << endl;
],[STDCXXLIBS="-lstdc++"
  AC_MSG_RESULT(yes)],[STDCXXLIBS=""
  AC_MSG_RESULT(no)])
AC_SUBST(STDCXXLIBS)
LIBS=$savedLibs
AC_LANG_RESTORE


dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_TYPE_SIGNAL
if test ${cross_compiling} = "no"; then
  AC_CHECK_LIB(m,cos)
fi
AC_CHECK_FUNCS(gethostname gettimeofday regcomp select socket)
AC_CHECK_FUNCS(strerror strstr cos sin socketpair)
AC_REPLACE_FUNCS(mkstemp localtime_r asctime_r)

AX_BOOST_BASE

AC_MSG_CHECKING(tcl build dir)
dnl AC_MSG_NOTICE([cross_compiling = ${cross_compiling}])
if test ${cross_compiling} = "yes"; then
  dnl AC_MSG_NOTICE([cross_compiling -- finding cross tcl: CC is $CC])
  gccbin=`which $CC`
  gccbindir=`dirname $gccbin`
  crossdir=`dirname $gccbindir`
  crossexec_prefix=$crossdir/${target_cpu}-${target_os}
  TCL_LIB_DIR=$crossexec_prefix/lib
else
  dirs="${BUILD_DIR}/../tcl8.3a*/unix \
	${BUILD_DIR}/../tcl8.3b*/unix \
	${BUILD_DIR}/../tcl8.4.*/unix \
	${BUILD_DIR}/../tcl8.5.*/unix \
	/usr/share /usr/lib64 /usr/lib"
  for i in $dirs ; do
    if test -d "$i" -a -f "$i/tclConfig.sh"; then
	TCL_LIB_DIR=`cd $i; pwd`
    fi
  done
fi
dnl AC_MSG_NOTICE([TCL_LIB_DIR initialized as $TCL_LIB_DIR])
AC_ARG_WITH(tcl, [  --with-tcl=DIR          use Tcl binaries from DIR], TCL_LIB_DIR=$withval,)
if test -d $TCL_LIB_DIR; then
    AC_MSG_RESULT([using tcl library in $TCL_LIB_DIR])
else
    AC_MSG_ERROR(Tcl directory $TCL_LIB_DIR doesn't exist)
fi

cd ${TCL_LIB_DIR}/..
TCL_TOP_DIR=`pwd`
cd ${BUILD_DIR}

#--------------------------------------------------------------------

file=$TCL_LIB_DIR/tclConfig.sh
. $file
SHLIB_CFLAGS=`echo "$TCL_SHLIB_CFLAGS" |sed -e "s|PIC|pic|" -e "s|+Z|+z|"`
SHLIB_LD=`echo $TCL_SHLIB_LD|sed "s/-Wl,-soname,libtcl.so.0//g"`
SHLIB_SUFFIX=$TCL_SHLIB_SUFFIX
DL_LIBS=$TCL_DL_LIBS
LD_FLAGS=$TCL_LD_FLAGS

TCL_BASE=`echo ${TCL_LIB_SPEC} |sed -e "s|^.*-l||"`
TCL_STUB_BASE=`echo ${TCL_STUB_LIB_SPEC} |sed -e "s|^.*-l||"`

AC_SUBST(TCL_LIB_DIR)
if test -f ${TCL_LIB_DIR}/tclsh${TCL_VERSION}; then
    TCL_BIN_DIR=${TCL_LIB_DIR}
elif test -f ${TCL_EXEC_PREFIX}/bin/tclsh${TCL_VERSION}; then
    TCL_BIN_DIR=${TCL_EXEC_PREFIX}/bin
elif test -f ${TCL_EXEC_PREFIX}/tclsh; then
    TCL_BIN_DIR=${TCL_EXEC_PREFIX}
elif test -f ${TCL_EXEC_PREFIX}/bin/tclsh; then
    TCL_BIN_DIR=${TCL_EXEC_PREFIX}/bin
else
    TCL_BIN_DIR=$exec_prefix/bin
fi
AC_SUBST(TCL_BIN_DIR)
if test -f ${TCL_BIN_DIR}/tclsh${TCL_VERSION}; then
    TCLSH=${TCL_BIN_DIR}/tclsh${TCL_VERSION}
else
    TCLSH=${TCL_BIN_DIR}/tclsh
fi
AC_SUBST(TCLSH)
if test -f ${TCL_BIN_DIR}/wish${TCL_VERSION}; then
    WISH=${TCL_BIN_DIR}/wish${TCL_VERSION}
else
    WISH=${TCL_BIN_DIR}/wish
fi
AC_SUBST(WISH)
if test -d ${TCL_TOP_DIR}/generic -a -f ${TCL_TOP_DIR}/generic/tcl.h; then
    TCL_INC_DIR=${TCL_TOP_DIR}/generic
elif test -d ${TCL_TOP_DIR}/include -a -f ${TCL_TOP_DIR}/include/tcl.h; then
    TCL_INC_DIR=${TCL_TOP_DIR}/include
elif test -d ${TCL_TOP_DIR}/include -a -f ${TCL_TOP_DIR}/include/tcl.h; then
    TCL_INC_DIR=${TCL_TOP_DIR}/include
elif test -d ${TCL_EXEC_PREFIX}/include -a -f ${TCL_EXEC_PREFIX}/include/tcl.h; then
    TCL_INC_DIR=${TCL_EXEC_PREFIX}/include
elif test -d ${TCL_EXEC_PREFIX}/include -a -f ${TCL_EXEC_PREFIX}/include/tcl.h; then
    TCL_INC_DIR=${TCL_EXEC_PREFIX}/include
elif test x"${TCL_INCLUDE_SPEC}" != x ; then
    d=`echo "${TCL_INCLUDE_SPEC}" | sed -e 's/^-I//'`
    TCL_INC_DIR=$d
else
    TCL_INC_DIR=$prefix/include
fi
AC_SUBST(TCL_INC_DIR)
if test -d ${TCL_TOP_DIR}/lib/tcl${TCL_VERSION} -a -f ${TCL_TOP_DIR}/lib/tcl${TCL_VERSION}/init.tcl; then
    TCL_LIBRARY=${TCL_TOP_DIR}/lib/tcl${TCL_VERSION}
elif test -d ${TCL_TOP_DIR}/lib/tcl${TCL_VERSION} -a -f ${TCL_TOP_DIR}/lib/tcl${TCL_VERSION}/init.tcl; then
    TCL_LIBRARY=${TCL_TOP_DIR}/lib/tcl${TCL_VERSION}
elif test -d ${TCL_TOP_DIR}/library -a -f ${TCL_TOP_DIR}/library/init.tcl; then
    TCL_LIBRARY=${TCL_TOP_DIR}/library
elif test -d ${TCL_PREFIX}/lib/tcl${TCL_VERSION} -a -f ${TCL_PREFIX}/lib/tcl${TCL_VERSION}/init.tcl; then
    TCL_LIBRARY=${TCL_PREFIX}/lib/tcl${TCL_VERSION}
elif test -d ${TCL_PREFIX}/lib/tcl${TCL_VERSION} -a -f ${TCL_PREFIX}/lib/tcl${TCL_VERSION}/init.tcl; then
    TCL_LIBRARY=${TCL_PREFIX}/lib/tcl${TCL_VERSION}
else
    TCL_LIBRARY=$prefix/lib/tcl${TCL_VERSION}
fi
AC_SUBST(TCL_LIBRARY)

TCL_LIB_SPEC="-L${TCL_LIB_DIR} -l${TCL_BASE}"
TCL_STUB_LIB_SPEC="-L${TCL_LIB_DIR} -l${TCL_STUB_BASE}"
eval "TCL_UNSHARED_LIB_FILE=lib${TCL_BASE}${TCL_UNSHARED_LIB_SUFFIX}"
eval "TCL_SHARED_LIB_FILE=lib${TCL_BASE}${TCL_SHARED_LIB_SUFFIX}"

#--------------------------------------------------------------------
LIB_INSTALL_DIR="${LIB_RUNTIME_DIR}"
#--------------------------------------------------------------------
#	The statements below define a collection of symbols related to
#	building libimg as a shared library instead of a static library.
#--------------------------------------------------------------------

TCL_SHARED_LIBS=""
#--------------------------------------------------------------------
AC_ARG_ENABLE(stubs, [  --disable-stubs         disable stub support for Img],
    [img_ok=$enableval], [img_ok=yes])
if test "$img_ok" != "no"; then
    TCL_DEFS="$TCL_DEFS -DUSE_TCL_STUBS"
    TCL_SHARED_LIBS="${TCL_STUB_LIB_SPEC}"
else
    case "$SHLIB_LD" in
	*ldAix*)
	TCL_SHARED_LIBS='${TCL_LIB_SPEC}'
	;;
    esac
fi

case "$SHLIB_LD" in
    *ldAix*)
    TCL_SHARED_LIBS="${TCL_SHARED_LIBS}"
    ;;
esac

if test ${cross_compiling} = "no"; then
  HOSTTCLSH=${TCLSH}
else
  AC_PATH_PROGS(HOSTTCLSH,[tclsh8.4 tclsh])
fi

dnl AC_MSG_CHECKING(bwidget dir)
dnl BWLIB=`echo 'puts $tcl_library'|${HOSTTCLSH}`
dnl dirs="${TCL_PREFIX}/lib/bwidget* ${TCL_PREFIX}/lib/tcllib*/bwidget* ${BWLIB}/bwidget* ${BWLIB}/../tcllib*/bwidget* ${BWLIB}/../bwidget* ${BWLIB}/tcllib*/bwidget*"
dnl for i in $dirs ; do
dnl   if test -d "$i" -a -f "$i/pkgIndex.tcl"; then
dnl     BWLIB=`cd $i; pwd`
dnl   fi
dnl done
dnl AC_ARG_WITH(bwlib, [  --with-bwlib=DIR          use BWidget from DIR], BWLIB=$withval,)
dnl if test -d $BWLIB; then
dnl    AC_MSG_RESULT([using BWidget library in $BWLIB])
dnl else
dnl    AC_MSG_ERROR(BWidget library directory $BWLIB doesn't exist)
dnl fi
dnl AC_SUBST(BWLIB)

AC_MSG_CHECKING(snit dir)
SNITLIB=`echo 'puts $tcl_library'|${HOSTTCLSH}`

dirs="${TCL_PREFIX}/lib/snit* ${TCL_PREFIX}/lib/tcllib*/snit* ${SNITLIB}/../tcllib*/snit* ${SNITLIB}/../snit* ${SNITLIB}/tcllib*/snit* ${SNITLIB}/snit*"
for i in $dirs ; do
  if test -d "$i" -a -f "$i/pkgIndex.tcl"; then
    SNITLIB=`cd $i; pwd`
  fi
done
AC_ARG_WITH(snitlib, [  --with-snitlib=DIR          use snit from DIR], SNITLIB=$withval,)
if test -d $SNITLIB; then
   AC_MSG_RESULT([using snit library in $SNITLIB])
else
   AC_MSG_ERROR(Snit library directory $SNITLIB doesn't exist)
fi
AC_SUBST(SNITLIB)

AC_MSG_CHECKING(csv dir)
CSVLIB=`echo 'puts $tcl_library'|${HOSTTCLSH}`

dirs="${TCL_PREFIX}/lib/csv* ${TCL_PREFIX}/lib/tcllib*/csv* ${CSVLIB}/../tcllib*/csv* ${CSVLIB}/../csv* ${CSVLIB}/tcllib*/csv* ${CSVLIB}/csv*"
for i in $dirs ; do
  if test -d "$i" -a -f "$i/pkgIndex.tcl"; then
    CSVLIB=`cd $i; pwd`
  fi
done
AC_ARG_WITH(csvlib, [  --with-csvlib=DIR          use csv from DIR], CSVLIB=$withval,)
if test -d $csvLIB; then
   AC_MSG_RESULT([using csv library in $CSVLIB])
else
   AC_MSG_ERROR(csv library directory $CSVLIB doesn't exist)
fi
AC_SUBST(CSVLIB)

AC_MSG_CHECKING(struct dir)
STRUCTLIB=`echo 'puts $tcl_library'|${HOSTTCLSH}`

dirs="${TCL_PREFIX}/lib/struct* ${TCL_PREFIX}/lib/tcllib*/struct* ${STRUCTLIB}/../tcllib*/struct* ${STRUCTLIB}/../struct* ${STRUCTLIB}/tcllib*/struct* ${STRUCTLIB}/struct*"
for i in $dirs ; do
  if test -d "$i" -a -f "$i/pkgIndex.tcl"; then
    STRUCTLIB=`cd $i; pwd`
  fi
done
AC_ARG_WITH(structlib, [  --with-structlib=DIR          use struct from DIR], STRUCTLIB=$withval,)
if test -d $STRUCTLIB; then
   AC_MSG_RESULT([using struct library in $STRUCTLIB])
else
   AC_MSG_ERROR(struct library directory $STRUCTLIB doesn't exist)
fi
AC_SUBST(STRUCTLIB)

AC_MSG_CHECKING(Tcl log dir)
TCLLOGLIB=`echo 'puts $tcl_library'|${HOSTTCLSH}`

dirs="${TCL_PREFIX}/lib/log* ${TCL_PREFIX}/lib/tcllib*/log* ${TCLLOGLIB}/../tcllib*/log* ${TCLLOGLIB}/../log* ${TCLLOGLIB}/tcllib*/log* ${TCLLOGLIB}/log*"
for i in $dirs ; do
  if test -d "$i" -a -f "$i/pkgIndex.tcl"; then
    TCLLOGLIB=`cd $i; pwd`
  fi
done
AC_ARG_WITH(tclloglib, [  --with-tclloglib=DIR          use tcl log from DIR], TCLLOGLIB=$withval,)
if test -d $TCLLOGLIB; then
   AC_MSG_RESULT([using tcl log library in $TCLLOGLIB])
else
   AC_MSG_ERROR(tcl log library directory $TCLLOGLIB doesn't exist)
fi
AC_SUBST(TCLLOGLIB)

AC_PATH_PROGS(UNZIP,unzip)

AC_MSG_CHECKING([Checking target Img and tclkit])
case $target_os in 
  macos | darwin) 
    AC_SUBST(IMG_ZIP, Img-1.4-Mac-Universal.zip)
    AC_SUBST(TCLKITRUN, tclkit-darwin-univ-aqua)
    ;;
  linux-gnu*)
    case $target_cpu in
      x86_64)
	AC_SUBST(IMG_ZIP, Img-1.4-Linux64.zip)
	AC_SUBST(TCLKITRUN, tclkit-linux-x86_64)
	;;
      i*86)
	AC_SUBST(IMG_ZIP, Img-1.4-Linux32.zip)
	AC_SUBST(TCLKITRUN, tclkit-linux-x86)
	;;
      arm*)
        AC_SUBST(IMG_ZIP, Img-1.3-Linuxarmv7l.zip)
        AC_SUBST(TCLKITRUN, tclkit-linux-armv7l)
        ;;
      *)
	AC_MSG_ERROR([Unsupported Linux arch: $target_cpu])
	;;
     esac
     ;;

   mingw32 | winnt | windows* | cygwin)
     AC_SUBST(IMG_ZIP, Img-1.4-win32.zip)
     AC_SUBST(TCLKITRUN, tclkit-win32.exe)
     AC_SUBST(EXEEXT, .exe)
     ;;
   *) 
     AC_MSG_ERROR([Unsupported Operating System: $target_os])
     ;;
esac
AC_MSG_RESULT([IMG_ZIP is $IMG_ZIP and TCLKITRUN is $TCLKITRUN])

AC_MSG_CHECKING([Checking build tclkit])
case $build_os in 
  macos | darwin) 
    AC_SUBST(TCLKITBSRC, tclkit-darwin-univ-aqua)
    AC_SUBST(BEXEEXT, )
    ;;
  linux-gnu*)
    AC_SUBST(BEXEEXT, )
    case $build_cpu in
      x86_64)
	AC_SUBST(TCLKITBSRC, tclkit-linux-x86_64)
	;;
      i*86)
	AC_SUBST(TCLKITBSRC, tclkit-linux-x86)
	;;
      arm*)
        AC_SUBST(TCLKITBSRC, tclkit-linux-armv7l)
        ;;
      *)
	AC_MSG_ERROR([Unsupported Linux arch: $build_cpu])
	;;
     esac
     ;;

   mingw32 | winnt | windows* | cygwin)
     AC_SUBST(BEXEEXT, .exe)
     AC_SUBST(TCLKITBSRC, tclkit-win32.exe)
     ;;
   *) 
     AC_MSG_ERROR([Unsupported Operating System: $build_os])
     ;;
esac
AC_MSG_RESULT([TCLKITBSRC is $TCLKITBSRC])


dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/ioctl.h sys/time.h syslog.h unistd.h sys/select.h)


AC_PATH_PROG(SED,sed)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.

AC_DEFINE_UNQUOTED(LOCALEDIR,"${prefix}/share/locale",[Location of the locale data files])
LOCALEDIR="${prefix}/share/locale"
AC_SUBST(LOCALEDIR)

dnl Substitutions...

AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(SHLIB_CFLAGS)
AC_SUBST(SHLIB_CXXFLAGS)
SHLIB_LD=`echo $SHLIB_LD|sed -e "s/${TCL_CC}/${CXX}/g"`
AC_SUBST(SHLIB_LD)
SHLIB_LD_LIBS=$TCL_SHLIB_LD_LIBS
AC_SUBST(SHLIB_LD_LIBS)
AC_SUBST(SHLIB_SUFFIX)
AC_SUBST(TCL_DBGX)
AC_SUBST(TCL_VERSION)
AC_SUBST(TCL_DEFS)
AC_SUBST(TCL_LIBS)
AC_SUBST(TCL_STUB_LIB_SPEC)
AC_SUBST(TCL_STUB_LIB_PATH)
AC_SUBST(TCL_SHARED_LIBS)
AC_SUBST(TCL_SHARED_LIB_FILE)
AC_SUBST(TCL_UNSHARED_LIB_FILE)
AC_SUBST(TCL_SHLIB_CFLAGS)
AC_SUBST(TCL_SHLIB_SUFFIX)
TCL_SHLIB_LD=`echo $TCL_SHLIB_LD|sed -e "s/${TCL_CC}/${CXX}/g"`
AC_SUBST(TCL_SHLIB_LD)
AC_SUBST(TCL_PREFIX)
AC_SUBST(TCL_EXEC_PREFIX)
AC_SUBST(TCL_CFLAGS)
AC_SUBST(TCL_LIB_SPEC)
AC_SUBST(TCL_INCLUDE_SPEC)
SYSTEMVERSION="$MRR_PATCH_LEVEL"
AC_SUBST(SYSTEMVERSION)

AC_CONFIG_HEADER([config.h])

AC_CONFIG_FILES([config_tcl.sed \
	  Makefile \
	  BuildScripts/Makefile \
	  BuildData/Makefile \
	  C++/Makefile \
		C++/RailDriver/Makefile \
		C++/RailDriver/Doxyfile \
		C++/ParserClasses/Makefile \
		C++/Azatrax/Makefile \
		C++/Azatrax/Doxyfile \
		C++/TTSupport/Makefile \
		C++/FCFSupport/Makefile \
	  Scripts/Makefile \
		Scripts/Common/Makefile \
		Scripts/GRSupport/Makefile \
		Scripts/Dispatcher/Makefile \
		Scripts/Dispatcher/Doxyfile \
		Scripts/CameraScripts/Makefile \
		Scripts/CameraScripts/Doxyfile \
		Scripts/CalcScripts/Makefile \
		Scripts/CalcScripts/Doxyfile \
		Scripts/FreightCarForwarder2/Makefile \
		Scripts/FreightCarForwarder2/Doxyfile \
		Scripts/FreightCarForwarder2/NorthAmerican/Makefile \
		Scripts/TimeTable2/Makefile \
		Scripts/TimeTable2/Doxyfile \
		Scripts/ControlSupport/Makefile \
		Scripts/MainProgram/Makefile \
		Scripts/GenericThrottle/Makefile \
		Scripts/GenericThrottle/Doxyfile \
		Scripts/CMri/Makefile \
		Scripts/CMri/Doxyfile \
		Scripts/XPressNet/Makefile \
		Scripts/XPressNet/Doxyfile \
		Scripts/NCE/Makefile \
		Scripts/LocoNet/Makefile \
		Scripts/RailDriverSupport/Makefile \
		Scripts/RailDriverSupport/Doxyfile \
		Scripts/Satellite/Makefile \
		Scripts/Satellite/Doxyfile \
	  Doc/Makefile \
		Doc/Support/Makefile \
		Doc/User/Makefile \
                Doc/User/Doxyfile.UserManual \
                Doc/User/Makefile.UserManual \
                Doc/User/Doxyfile.TimeTable \
                Doc/User/Doxyfile.FCF \
                Doc/User/Doxyfile.Calc \
                Doc/User/Doxyfile.Camera \
                Doc/User/Doxyfile.Dispatcher \
                Doc/User/Doxyfile.AzatraxProg \
                Doc/User/Doxyfile.GenericThrottle \
                Doc/User/Doxyfile.MainProgram \
                Doc/User/Doxyfile.UniversalTest \
                Doc/User/Doxyfile.XPressNetThrottle \
		Doc/doxygen/Makefile \
		Doc/doxygen/Doxyfile \
		Doc/doxygen/tcldoxyfilter.sh \
		Doc/ProgrammingGuides/Makefile \
		Doc/ProgrammingGuides/Doxyfile.SampleCode \
		Doc/ApplicationNotes/Makefile \
	  Installer/Makefile \
	  intl/Makefile \
	  po/Makefile \
	  	po/CalcScripts/Makefile \
	  	po/CameraScripts/Makefile \
	  	po/Dispatcher/Makefile \
	  	po/FreightCarForwarder2/Makefile \
	  	po/TimeTable2/Makefile \
	  	po/GenericThrottle/Makefile \
	  	po/UniversalTest/Makefile \
	  	po/XPressNetThrottle/Makefile \
	  	po/AzatraxProg/Makefile \
	  	po/mrrsystem/Makefile \
		po/mrrsystem/mrrsystem_header.pot \
		po/raildriverd/Makefile \
		po/Satellite/Makefile \
    		po/xplocotest/Makefile \
          ExampleDataAndCode/Makefile \
                ExampleDataAndCode/Blocks/Makefile \
                ExampleDataAndCode/Blocks/Doxyfile \
                ExampleDataAndCode/Signals/Makefile \
                ExampleDataAndCode/Signals/Doxyfile \
                ExampleDataAndCode/InterlockingPlants/Makefile \
                ExampleDataAndCode/InterlockingPlants/Doxyfile \
                ExampleDataAndCode/Yards/Makefile \
                ExampleDataAndCode/Yards/Doxyfile \
                ExampleDataAndCode/Sidings/Makefile \
                ExampleDataAndCode/Sidings/Doxyfile \
                ExampleDataAndCode/Switches/Makefile \
                ExampleDataAndCode/Switches/Doxyfile \
                ExampleDataAndCode/TimeTables/Makefile \
                ExampleDataAndCode/FreightCarForwarding/Makefile \
	  MRRSystem.spec \
	  BuildBinaryDistro.sh \
	  Doxyfile])
AC_CONFIG_COMMANDS([default],[chmod +x BuildBinaryDistro.sh])
AC_OUTPUT


